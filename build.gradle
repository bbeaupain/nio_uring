plugins {
    id 'java'
    id 'c'
    id 'jacoco'
    id 'maven'
    id 'signing'
}

group 'sh.blake.niouring'
version '0.1.0'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

test {
    testLogging.showStandardStreams = true
    systemProperty "java.library.path", file("${buildDir}/libs/nio_uring/shared").absolutePath
}

jacoco {
    toolVersion = "0.8.2"
}

processResources {
    from "${buildDir}/libs/nio_uring/shared/"
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
            repository(url: "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }
            snapshotRepository(url: "https://s01.oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }
            pom.project {
                name = "nio_uring"
                description = "I/O library for Java using io_uring under the hood"
                url = "https://github.com/bbeaupain/nio_uring"
                inceptionYear = "2022"
                packaging = "jar"
                licenses {
                    license {
                        name = "The MIT License"
                        url = "https://opensource.org/licenses/MIT"
                        distribution = "repo"
                    }
                }
                developers {
                    developer {
                        name = "Blake Beaupain"
                        email = "me@blake.sh"
                        organization = "Blake Beaupain"
                        organizationUrl = "https://www.blake.sh"
                    }
                }
                scm {
                    connection = "scm:git:git://github.com/bbeaupain/nio_uring.git"
                    developerConnection = "scm:git:git://github.com/bbeaupain/nio_uring.git"
                    url = "https://github.com/bbeaupain/nio_uring"
                }
            }
        }
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

signing {
    sign configurations.archives
}

model {
    binaries {
        all {
            if (toolChain in Gcc) {
                cCompiler.args "-O2"
            }
        }
    }

    repositories {
        libs(PrebuiltLibraries) {
            jdk {
                headers.srcDirs "${System.properties['java.home']}/include",
                        "${System.properties['java.home']}/include/win32",
                        "${System.properties['java.home']}/include/darwin",
                        "${System.properties['java.home']}/include/linux"
            }

            liburing {
                headers.srcDirs "${System.getenv("LIBURING_PATH")}/src",
                        "${System.getenv("LIBURING_PATH")}/src/include",
                        "${System.getenv("LIBURING_PATH")}/src/include/liburing"
                binaries.withType(PrebuiltStaticLibraryBinary) {
                    staticLibraryFile = file("${System.getenv("LIBURING_PATH")}/src/liburing.a")
                }
            }
        }
    }

    platforms {
        x64 { architecture "x86_64" }
        x86 { architecture "x86" }
    }

    components {
        nio_uring(NativeLibrarySpec) {
            sources {
                c {
                    source {
                        lib library: 'nio_uring', linkage: 'static'
                        lib library: 'liburing', linkage: 'static'
                        lib library: 'jdk', linkage: 'api'
                        srcDir "src/main/c"
                        include "**/*.c"
                    }
                }
            }
        }
    }
}

build.dependsOn("nio_uringSharedLibrary")
test.dependsOn("nio_uringSharedLibrary")
processResources.dependsOn("nio_uringSharedLibrary")